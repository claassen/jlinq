buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'signing'
apply plugin: 'maven'

sourceCompatibility = '1.8'

group = "com.github.claassen"
version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

if(project.hasProperty("release")) {

    signing {
        sign configurations.archives
    }

    //https://oss.sonatype.org
    uploadArchives {
        repositories {
            mavenDeployer {
                // POM signature
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                // Target repository
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUser, password: ossrhPassword)
                }
                pom.project {
                    name 'JLinq'
                    description 'A library for querying collections in a nicer way than the Java 8 Stream API'
                    packaging 'jar'
                    url 'https://github.com/claassen/jlinq'

                    scm {
                        connection 'scm:git:https://github.com/claassen/jlinq.git'
                        developerConnection 'scm:git:git@github.com:claassen/jlinq.git'
                        url 'https://github.com/claassen/jlinq.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'claassen'
                            name = 'Michael Claassen'
                            email = 'mk.clssn@gmail.com'
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        username = ossrhUser
        password = ossrhPassword
        packageGroup = "com.github.claassen"
    }
}

